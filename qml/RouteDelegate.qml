/*
 * This file is part of the Meegopas, more information at www.gitorious.org/meegopas
 *
 * Author: Jukka Nousiainen <nousiaisenjukka@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * See full license at http://www.gnu.org/licenses/gpl-3.0.html
 */

import QtQuick 2.1
import Sailfish.Silica 1.0
import "reittiopas.js" as Reittiopas
import "colors.js" as Colors

BackgroundItem {
    id: routeDelegate
    height: type != "walk" ? Theme.itemSizeMedium : Theme.itemSizeExtraSmall
    width: parent.width

    onClicked: {
        pageStack.push(Qt.resolvedUrl("StopPage.qml"),{ leg_index: leg_number, leg_code: code })
    }

    Label {
        id: length_text
        text: type == "walk"? Math.floor(length/100)/10 + " km" : duration + " min"
        font.pixelSize: Theme.fontSizeSmall
        color: Theme.secondaryColor
        anchors.verticalCenter: parent.verticalCenter
    }

    Column {
        id: rect
        anchors.left: length_text.right
        anchors.verticalCenter: parent.verticalCenter
        width: 20
        Rectangle {
            anchors.right: parent.right
            width: 15
            height: 5
            color: Colors.colors[type]
        }

        Rectangle {
            anchors.right: parent.right
            width: 5
            height: routeDelegate.height + Theme.paddingSmall
            color: Colors.colors[type]
        }

        Rectangle {
            anchors.right: parent.right
            width: 15
            height: 5
            color: Colors.colors[type]
        }
    }

    Column {
        id: transportColumn
        anchors.left: rect.left
        anchors.verticalCenter: parent.verticalCenter
        anchors.leftMargin: Theme.paddingLarge
        width: 100

        Image {
            visible: type != "walk"
            anchors.horizontalCenter: parent.horizontalCenter
            source: "qrc:/images/" + type + ".png"
            height: 50
            width: height
            smooth: true
        }

        Label {
            visible: type != "walk"
            text: code
            font.pixelSize: Theme.fontSizeSmall
            color: Theme.primaryColor
            anchors.horizontalCenter: parent.horizontalCenter
        }
    }

}
